plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "de.undercouch.download" version "3.4.3"
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    group 'com.deflatedpickle.whootm'

    sourceCompatibility = 1.8

    idea {
        module {
            sourceDirs += file("main/kotlin")
            resourceDirs += file("main/resources")
        }
    }

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        implementation 'org.apache.commons:commons-lang3:3.9'
    }
}

dependencies {
    subprojects.forEach {
        archives(it)
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

project("shell") {
    version '0.1.0'

    application {
        mainClassName = 'com.deflatedpickle.whootm.MainKt'
    }

    shadowJar {
        minimize()
        archiveName = "Whoosh.jar"
    }

    dependencies {
        // Fuzzy searching
        implementation 'me.xdrop:fuzzywuzzy:1.2.0'
        // ANSI code wrapper
        implementation 'com.github.fusesource.jansi:jansi:jansi-project-1.17'
        // TermIOS wrapper
        implementation 'com.github.purejavacomm:purejavacomm:1.0.2.RELEASE'
        // ASCII tables
        implementation 'de.vandermeer:asciitable:0.3.2'

        // Apache Commons
        implementation 'commons-io:commons-io:20030203.000550'
        // Apache Tika
        implementation 'org.apache.tika:tika-core:1.23'
    }
}

project("terminal") {
    version '0.1.0'

    application {
        mainClassName = 'com.deflatedpickle.whootm.MainKt'
    }

    shadowJar {
        minimize()
        archiveName = "WhooTerm.jar"
    }

    def osgi_platform = ''
    def swt_version = '3.105.3'

    def os = System.getProperty("os.name").toLowerCase()
    if (os.contains("windows")) {
        osgi_platform = 'win32.win32.x86_64'
    } else if (os.contains("linux")) {
        osgi_platform = 'gtk.linux.x86_64'
    }

    dependencies {
        compile("org.eclipse.platform:org.eclipse.swt.$osgi_platform:$swt_version") {
            transitive = false
        }
        compile "org.eclipse.platform:org.eclipse.jface:3.12.2"

        compile group: 'net.java.dev.jna', name: 'jna', version: '5.2.0'
        compile 'net.java.dev.jna:jna-platform:5.2.0'

        implementation 'org.lwjgl.osgi:org.lwjgl.opengl:3.2.0'
        implementation "org.joml:joml:1.9.12"

        implementation 'com.google.code.gson:gson:2.8.5'

        compile files("libs/freetype-jni.jar")
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.$osgi_platform:$swt_version")
            }
        }
    }
}